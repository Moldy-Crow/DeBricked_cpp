cmake_minimum_required(VERSION 3.27)

set(RICHARD_CLI_TARGET richardcli)
set(RICHARD_CLI_TARGET ${RICHARD_CLI_TARGET} PARENT_SCOPE)

find_package(Boost COMPONENTS program_options REQUIRED)

file(GLOB
  CPP_SOURCES
  "${CMAKE_CURRENT_SOURCE_DIR}/src/*.cpp"
)

add_executable(${RICHARD_CLI_TARGET} ${CPP_SOURCES})

set(CPU_PROFILE_COMPILE_FLAGS -g)

if(CMAKE_SYSTEM_NAME STREQUAL "Linux")
  set(COMPILE_FLAGS -Wextra -Wall)
  if (CPU_PROFILE)
    set(COMPILE_FLAGS ${COMPILE_FLAGS} ${CPU_PROFILE_COMPILE_FLAGS})
  endif()
  set(DEBUG_COMPILE_FLAGS ${COMPILE_FLAGS} -g)
  set(RELEASE_COMPILE_FLAGS ${COMPILE_FLAGS} -O3)

  set(CPU_PROFILE_LINK_FLAGS -Wl,--no-as-needed -lprofiler -Wl,--as-needed)
  set(LINK_FLAGS)
  if (CPU_PROFILE)
    set(LINK_FLAGS ${LINK_FLAGS} ${CPU_PROFILE_LINK_FLAGS})
  endif()
  set(DEBUG_LINK_FLAGS ${LINK_FLAGS})
  set(RELEASE_LINK_FLAGS ${LINK_FLAGS})
elseif(CMAKE_SYSTEM_NAME STREQUAL "Windows")
  set(COMPILE_FLAGS /W4)
  set(DEBUG_COMPILE_FLAGS ${COMPILE_FLAGS})
  set(RELEASE_COMPILE_FLAGS ${COMPILE_FLAGS} /O2)
endif()

target_compile_options(${RICHARD_CLI_TARGET}
  PRIVATE
    "$<$<CONFIG:DEBUG>:${DEBUG_COMPILE_FLAGS}>"
    "$<$<CONFIG:RELEASE>:${RELEASE_COMPILE_FLAGS}>"
)

target_link_libraries(${RICHARD_CLI_TARGET}
  PRIVATE
    ${RICHARD_LIB_TARGET}
    Boost::program_options
    "$<$<CONFIG:DEBUG>:${DEBUG_LINK_FLAGS}>"
    "$<$<CONFIG:RELEASE>:${RELEASE_LINK_FLAGS}>"
)
